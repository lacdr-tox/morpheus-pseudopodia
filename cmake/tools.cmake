FUNCTION(list_prepend IN prefix OUT)
	SET(_list "")
	FOREACH(entry ${${IN}})
		LIST(APPEND _list "${prefix}${entry}")
	ENDFOREACH()
	SET(${OUT} ${_list} PARENT_SCOPE)
ENDFUNCTION()

## Add sources paths 
IF(POLICY CMP0076)
	CMAKE_POLICY(SET CMP0076 NEW)
ENDIF()
FUNCTION(target_sources_relpaths target iface sources)
	IF (CMAKE_VERSION VERSION_LESS 3.13)
		FOREACH(entry ${${sources}})
			target_sources(${target} ${iface} ${CMAKE_CURRENT_SOURCE_DIR}/${entry})
		ENDFOREACH()
	ELSE()
		target_sources(${target} ${iface} ${${sources}})
	ENDIF()
ENDFUNCTION()

## Add link libraries to a target, includes fixed handling for OBJECT_LIBRARY
## The fixed version provides proper treatment of object link libraries. 
## TODO Linker flags are not copied though.
FUNCTION(target_link_libraries_patched target iface)
	set(libraries ${ARGN})
	if (CMAKE_VERSION VERSION_LESS 3.12)
		get_property(target_type TARGET ${target} PROPERTY TYPE)
		FOREACH(library ${libraries})
			SET(LIB_IS_OBJECT FALSE)
			IF(TARGET ${library})
				add_dependencies(${target} ${library})
				get_property(type TARGET ${library} PROPERTY TYPE)
				IF(${type} STREQUAL "OBJECT_LIBRARY")
					SET(LIB_IS_OBJECT TRUE)
				ENDIF()
			ENDIF()
			IF(LIB_IS_OBJECT)
				## Library is an object library target
				# Linking
# 				message("${library} is an object lib to be added to ${target}" )
				IF(${iface} STREQUAL "PRIVATE" OR ${iface} STREQUAL "PUBLIC")
					target_sources(${target} PRIVATE $<TARGET_OBJECTS:${library}> )
					target_link_libraries(${target} PRIVATE $<TARGET_PROPERTY:${library},INTERFACE_LINK_LIBRARIES> )
					SET_PROPERTY(TARGET ${target} APPEND PROPERTY INCLUDE_DIRECTORIES $<TARGET_PROPERTY:${library},INCLUDE_DIRECTORIES> )
					SET_PROPERTY(TARGET ${target} APPEND PROPERTY COMPILE_OPTIONS $<TARGET_PROPERTY:${library},COMPILE_OPTIONS> )
				ENDIF()
				# Publishing
				IF(${iface} STREQUAL "PUBLIC" OR ${iface} STREQUAL "INTERFACE")
					SET_PROPERTY(TARGET ${target} APPEND PROPERTY INTERFACE_LINK_LIBRARIES $<TARGET_PROPERTY:${library},INTERFACE_LINK_LIBRARIES> )
					SET_PROPERTY(TARGET ${target} APPEND PROPERTY INTERFACE_INCLUDE_DIRECTORIES $<TARGET_PROPERTY:${library},INTERFACE_INCLUDE_DIRECTORIES> )
					SET_PROPERTY(TARGET ${target} APPEND PROPERTY INTERFACE_COMPILE_OPTIONS $<TARGET_PROPERTY:${library},INTERFACE_COMPILE_OPTIONS> )
				ENDIF()
				# get_property(DIRS TARGET ${library} INCLUDE_DIRECTORIES)
				# message("INCLUDE_DIRECTORIES of target ${target} are ${DIRS}")
			ELSE()
				IF(${target_type} STREQUAL "OBJECT_LIBRARY")
					# Forward link libraries 
					SET_PROPERTY(TARGET ${target} APPEND PROPERTY INTERFACE_LINK_LIBRARIES ${library})
					IF(TARGET ${library})
						SET_PROPERTY(TARGET ${target} APPEND PROPERTY INCLUDE_DIRECTORIES $<TARGET_PROPERTY:${library},INTERFACE_INCLUDE_DIRECTORIES> )
						SET_PROPERTY(TARGET ${target} APPEND PROPERTY COMPILE_OPTIONS $<TARGET_PROPERTY:${library},INTERFACE_COMPILE_OPTIONS> )
# 						IF(${iface} STREQUAL "PUBLIC" OR ${iface} STREQUAL "INTERFACE")
# 							SET_PROPERTY(TARGET ${target} APPEND PROPERTY INTERFACE_COMPILE_OPTIONS $<TARGET_PROPERTY:${library},INTERFACE_COMPILE_OPTIONS> )
# 							SET_PROPERTY(TARGET ${target} APPEND PROPERTY INTERFACE_INCLUDE_DIRECTORIES $<TARGET_PROPERTY:${library},INTERFACE_INCLUDE_DIRECTORIES> )
# 						ENDIF()
					ENDIF()
				ELSE()
					target_link_libraries(${target} ${iface} ${library})
				ENDIF()
			ENDIF()
			
		ENDFOREACH()
	ELSE()
		target_link_libraries(${target} ${iface} ${libraries})
		FOREACH(library ${libraries})
			IF( TARGET ${library} )
				get_property(type TARGET ${library} PROPERTY TYPE)
				IF(${type} STREQUAL "OBJECT_LIBRARY")
					# Library is an object library target
					## Need to add the interface libraries manually, since that is apparently not done
					IF(${iface} STREQUAL "PUBLIC" OR ${iface} STREQUAL "PRIVATE")
						target_link_libraries(${target} PRIVATE $<TARGET_PROPERTY:${library},INTERFACE_LINK_LIBRARIES> )
					ENDIF()
				ENDIF()
			ENDIF()
		ENDFOREACH()
	ENDIF()
ENDFUNCTION()



## Get all properties that cmake supports
execute_process(COMMAND cmake --help-property-list OUTPUT_VARIABLE CMAKE_PROPERTY_LIST)
## Convert command output into a CMake list
STRING(REGEX REPLACE ";" "\\\\;" CMAKE_PROPERTY_LIST "${CMAKE_PROPERTY_LIST}")
STRING(REGEX REPLACE "\n" ";" CMAKE_PROPERTY_LIST "${CMAKE_PROPERTY_LIST}")
list(REMOVE_DUPLICATES CMAKE_PROPERTY_LIST)

function(dump_target_properties tgt)
    if(NOT TARGET ${tgt})
      message("There is no target named '${tgt}'")
      return()
    endif()

    foreach (prop ${CMAKE_PROPERTY_LIST})
        string(REPLACE "<CONFIG>" "${CMAKE_BUILD_TYPE}" prop ${prop})
        IF(${prop} MATCHES "^(VS_|XCODE_).*")
			continue()
		ENDIF()
		IF(${prop} MATCHES "LOCATION")
			continue()
		ENDIF()
        get_target_property(propval ${tgt} ${prop})
        if (propval)
            get_target_property(propval ${tgt} ${prop})
            message ("${tgt} ${prop} = ${propval}")
        endif()
    endforeach(prop)
endfunction(dump_target_properties)
