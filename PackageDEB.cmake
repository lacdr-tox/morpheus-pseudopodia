STRING(TOLOWER CPACK_DEBIAN_PACKAGE_NAME "${CPACK_PACKAGE_NAME}")
STRING(TOLOWER CPACK_DEBIAN_FILE_NAME "${CPACK_PACKAGE_FILE_NAME}")
IF(CMAKE_VERSION VERSION_LESS "3.16")
	SET(CPACK_DEBIAN_PACKAGE_DESCRIPTION "${CPACK_PACKAGE_DESCRIPTION_SUMMARY}\n${CPACK_PACKAGE_DESCRIPTION}")
	STRING(REGEX REPLACE "\n" "\n " CPACK_DEBIAN_PACKAGE_DESCRIPTION "${CPACK_DEBIAN_PACKAGE_DESCRIPTION}")
	# Replace blank lines with . control statement
	STRING(REGEX REPLACE "\n +\n" "\n .\n" CPACK_DEBIAN_PACKAGE_DESCRIPTION "${CPACK_DEBIAN_PACKAGE_DESCRIPTION}")
ELSE()
	# CMAKE auto-assembles CPACK_DEBIAN_PACKAGE_DESCRIPTION
ENDIF()
SET(CPACK_DEBIAN_PACKAGE_SECTION "science")
SET(CPACK_DEBIAN_PACKAGE_MAINTAINTER ${CPACK_PACKAGE_CONTACT})

## Add target for creating the compressed changelog
add_custom_command( OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/changelog.gz
  COMMAND gzip -c -n -9 -f  ${CMAKE_CURRENT_SOURCE_DIR}/CHANGELOG.md >  ${CMAKE_CURRENT_BINARY_DIR}/changelog.gz
  COMMENT "Creating compressed changelog."
  DEPENDS ${CMAKE_CURRENT_SOURCE_DIR}/CHANGELOG.md
)

add_custom_target(DEB_EXTRAS ALL
	DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/changelog.gz)

INSTALL( FILES ${CMAKE_CURRENT_BINARY_DIR}/changelog.gz
	DESTINATION "share/doc/${CPACK_PACKAGE_NAME}/"
 	COMPONENT Morpheus_DEB_EXTRAS
)

INSTALL( FILES LICENSE.md
	RENAME copyright
	DESTINATION "share/doc/${CPACK_PACKAGE_NAME}/"
 	COMPONENT Morpheus_DEB_EXTRAS
)


## Set Package Dependencies 
SET(CPACK_DEBIAN_PACKAGE_SHLIBDEPS ON) 
SET(CPACK_DEBIAN_PACKAGE_GENERATE_SHLIBS_POLICY ">=") 
SET(CPACK_DEBIAN_PACKAGE_DEPENDS "gnuplot (>= 4.4)") 
IF (MORPHEUS_GUI) 
  MESSAGE(STATUS "Adding GUI dependencies" ) 
  SET(CPACK_DEBIAN_PACKAGE_DEPENDS  "${CPACK_DEBIAN_PACKAGE_DEPENDS}, libqt5sql5-sqlite") 
ENDIF() 
IF (MORPHEUS_GRAPHVIZ STREQUAL "Library") 
  MESSAGE(STATUS "Adding GraphVIZ dependencies" ) 
  SET(CPACK_DEBIAN_PACKAGE_DEPENDS "${CPACK_DEBIAN_PACKAGE_DEPENDS}, graphviz") 
ENDIF()
